"Name"    = 'VATU' 
"Version" = '2015'
"Author"  = 'Arsslen Idadi' 
"About"   = 'ANSI-C variant.'

"Case Sensitive" = True
"Start Symbol"   = <GLOBALS>

{Hex Digit}      = {Digit} + [abcdefABCDEF]
{Oct Digit}      = [01234567]
{Binary Digit}   = [01]
{Literal Suffix} = [iusbaIUSBA]
{Literal Prefix} = [-]
                                                  
{Id Head}        = {Letter} + [_]
{Id Tail}        = {Id Head} + {Digit}

{String Ch}      = {Printable} - ["]
{Char Ch}        = {Printable} - ['']

DecLiteral       = ({Literal Prefix} [123456789]{digit}* | {Literal Prefix}[123456789]{digit}* {Literal Suffix} | [123456789]{digit}* |[123456789]{digit}* {Literal Suffix})
OctLiteral       = 0{Oct Digit}*
HexLiteral       = 0x{Hex Digit}+
BinaryLiteral    = 0b{Binary Digit}+
             
BooleanLiteral   = true | false
NullLiteral      = null


StringLiteral    = '"'( {String Ch} | '\'{Printable} )* '"'                 
CharLiteral      = '' ( {Char Ch} | '\'{Printable} )''

Id               = {Id Head}{Id Tail}*

MacroLiteral     = '$' {Id Head}{Id Tail}*  
                 
!AsmLiteral       = '$>'  ( {String Ch} | '\'{Printable} )* '<$'


! ===================================================================
! Comments
! ===================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'


! ===================================================================
! Import
! ===================================================================

<Import>   ::= 'use' Id ';'
            
<Imports>  ::= <Import>
            | <Import> <Imports>   
                   
<Namespace> ::= 'namespace' Id ';'
            
              
! ===================================================================
!  Declaration
! ===================================================================
<GLOBALS> ::= <GLOBAL> <GLOBALS>
           | 
           
<GLOBAL> ::= <Decls>
          | <Namespace> <Imports> <Decls>
          | <Imports> <Decls>
          | <Namespace> <Decls>
          
<Decls> ::= <Decl> <Decls>
         | <Decl>

<Decl>  ::= <Func Decl>
          | <ASM Decl>
          | <Oper Decl>
          | <Inter Decl>
          | <Union Decl>
          | <Func Proto>
          | <Oper Proto>
          | <Struct Decl>
          | <Enum Decl>
          | <Var Decl>    
          | <Typedef Decl>
          | <Extension Decl>
          | <Delegate Decl>
          | <Preproc Decl>


! ===================================================================
! Preprocessor  
! ===================================================================      

<Preproc Decl> ::= <PP Define> 
                 | <PP Conditional>
                 | <PP Diag>
                 | <PP Region>

           
<PP Define> ::= '#'define ID <PP Expr>
               | '#'undef ID
               
                
<PP Expr> ::= <PP Or Expr>
           
<PP Or Expr> ::= <PP Or Expr> '||' <PP And Expr>   
               | <PP And Expr>   

<PP And Expr>     ::= <PP And Expr>    '&&' <PP Compare Expr>
               | <PP Compare Expr>                

<PP Compare Expr> ::= <PP Compare Expr> '==' <PP Unary Expr>
               | <PP Compare Expr> '!='  <PP Unary Expr>
               | <PP Unary Expr>

<PP Unary Expr> ::= '!'    <PP Unary Expr> 
                 | <PP Primary Expr>
                 
<PP Primary Expr> ::= <CONSTANT> 
                   | '(' <PP Expr> ')'
                   | Id
                   
<PP Conditional> ::= <PP If>
                    
!if elif elif endif                    
<PP If> ::= '#'if <PP Expr> '{' <Decls> '}' <PP Elif List> 
            | '#'if <PP Expr> '{' <Decls> '}'
            | '#'if <PP Expr> '{' <Decls> '}' <PP Elif List>  '#'else '{' <Decls>  '}'   
             | '#'if <PP Expr>  '{'  <Decls> '}' '#'else  '{'  <Decls>  '}'
             
            
<PP Elif List> ::=   <PP Elif> <PP Elif List> 
              |   <PP Elif> 
              

<PP Elif> ::= '#'elif <PP Expr> '{' <Decls> '}'

            
<PP Diag> ::= '#'warn StringLiteral
            | '#'error StringLiteral
 
<PP Region> ::= '#'region StringLiteral  <Decls>  '#'endregion
             
                           
! ===================================================================
! Extension  Declaration
! ===================================================================      

<Extension Decl> ::= <Value> extends <Type> ';'
                  
! ===================================================================
! Delegate  Declaration
! ===================================================================

<Delegate Decl> ::= <CallCV> delegate <Type> ID '(' <Types>  ')' ';'
                  
! ===================================================================
! Function  Declaration
! ===================================================================


<Func Proto> ::= <Func ID> '(' <Types>  ')' ';'
               | <Func ID> '(' <Params> ')' ';'
               | <Func ID> '(' ')' ';'
               ! Extension prototype
               | <Func ID> '(' <Types>  ')' <Func Ext> ';'
               | <Func ID> '(' <Params> ')' <Func Ext> ';'
               | <Func ID> '(' ')' <Func Ext> ';'
               
<Oper Proto> ::= 'override' <Type> 'operator' '=='  <Type>   ';'
               | 'override' <Type> 'operator' '!='  <Type>  ';'
               | 'override' <Type> 'operator' '<='  <Type>   ';'
               | 'override' <Type> 'operator' '>='  <Type> ';'
               | 'override' <Type> 'operator' '>'  <Type>   ';'
               | 'override' <Type> 'operator' '<'  <Type>  ';'
               | 'override' <Type> 'operator' '¤' <Type>  ';'
               | 'override' <Type> 'operator' '??' <Type>  ';'
               
               | 'override' <Type> 'operator' '+' ';'
               | 'override' <Type> 'operator' '-' ';'
               | 'override' <Type> 'operator' '*' ';'
               | 'override' <Type> 'operator' '/' ';'
               | 'override' <Type> 'operator' '%' ';'
               | 'override' <Type> 'operator' '^' ';'
               | 'override' <Type> 'operator' '&' ';'
               | 'override' <Type> 'operator' '|' ';'
                ! CAST Overload
               | 'override' <Type> 'operator' <Type> ';'
               !Shift Rotate
               | 'override' <Type> 'operator' '~>' ';'
               | 'override' <Type> 'operator' '<~' ';'
               | 'override' <Type> 'operator' '>>' ';'
               | 'override' <Type> 'operator' '<<' ';'
               !Unary
               | 'override' <Type> 'operator' '++' ';'
               | 'override' <Type> 'operator' '--' ';'
               | 'override' <Type> 'operator' '[]' ';'

               | 'override' <Type> 'operator' '~' ';'
              
<Func Decl>  ::= <Func ID> <Func Body>
               | <Func Spec> <Func ID> <Func Body>



!Operators Overloading         
<Oper Decl>  ::= 'override' <Type> 'operator' '==' <Func Body>
               | 'override' <Type> 'operator' '!=' <Func Body>
               | 'override' <Type> 'operator' '<=' <Func Body>
               | 'override' <Type> 'operator' '>=' <Func Body>
               | 'override' <Type> 'operator' '>' <Func Body>
               | 'override' <Type> 'operator' '<' <Func Body>
               | 'override' <Type> 'operator' '+' <Func Body>
               | 'override' <Type> 'operator' '-' <Func Body>
               | 'override' <Type> 'operator' '*' <Func Body>
               | 'override' <Type> 'operator' '/' <Func Body>
               | 'override' <Type> 'operator' '%' <Func Body>
               | 'override' <Type> 'operator' '^' <Func Body>
               | 'override' <Type> 'operator' '&' <Func Body>
               | 'override' <Type> 'operator' '|' <Func Body>
                ! CAST Overload
               | 'override' <Type> 'operator' <Type> <Func Body>
               !Shift Rotate
               | 'override' <Type> 'operator' '~>' <Func Body>
               | 'override' <Type> 'operator' '<~' <Func Body>
               | 'override' <Type> 'operator' '>>' <Func Body>
               | 'override' <Type> 'operator' '<<' <Func Body>
               !Unary
               | 'override' <Type> 'operator' '++' <Func Body>
               | 'override' <Type> 'operator' '--' <Func Body>
               | 'override' <Type> 'operator' '[]' <Func Body>
               | 'override' <Type> 'operator' '¤' <Func Body>
               | 'override' <Type> 'operator' '??' <Func Body>
               | 'override' <Type> 'operator' '~' <Func Body>
               
!Interrupt decl               
<Inter Decl>  ::= 'interrupt' <CONSTANT> <Block>


<Func Body>  ::= '(' <Params>  ')' <Block>
              | '(' ')' <Block>
              |  '(' <Params>  ')' <Func Ext> <Block>
              | '(' ')' <Func Ext>  <Block>      
              
        
<Params>     ::= <Param> ',' <Params>
               | <Param>
               
<Param>      ::= const <Type> ID
               | ref <Type> ID
               |       <Type> ID
               
<Types>      ::= <Type>  ',' <Types>
               | <Type> 
  
<Func ID>    ::= <Mod> <CallCV> <Type> ID
                | <Mod> <Type> ID
                | <CallCV> <Type> ID
                | <Type> ID

<Func Ext>  ::= extends <Type>
             | static extends <Type>
             
<Func Spec> ::= entry
             | isolated
             
<CallCV>    ::= stdcall 
             | fastcall
             | cdecl 
             | pascal
             | default
             | vfastcall
! ===================================================================
! Asm Declaration
! ===================================================================
<ASM Decl> ::= extern asm '{' <INSTRUCTIONS>  '}'
             | default asm '{' <INSTRUCTIONS>  '}'
           
! ===================================================================
! Type Declaration
! ===================================================================

<Typedef Decl> ::= typedef <Type> ID ';'

<Struct Decl>  ::= struct Id '{' <Struct Def> '}'  ';' 
                
<Union Decl>   ::= union Id '{' <Struct Def> '}'  ';' 

<Struct Def>   ::= <Var Decl> <Struct Def>
                 | <Var Decl>

! ===================================================================
! Variable Declaration
! ===================================================================

<Var Decl>     ::= <Mod> <Type> <Var> <Var List>  ';'
                 |       <Type> <Var> <Var List>  ';'
                 | <Mod>        <Var> <Var List>  ';'
             
<Var>      ::= ID <Array>
             | ID
             | ID <Array> '=' <Op If> 
             | ID '=' <Op If> 

<Array>    ::= '[' <Expression> ']'
             | '[' ']'
             
<Var List> ::=  ',' <Var Item> <Var List>
             | 

<Var Item> ::= <Pointers> <Var>

             
<Mod>      ::= extern 
             | static
             | const   
             | private
             | public
             

! ===================================================================
! Enumerations
! ===================================================================

<Enum Decl>    ::= enum Id '{' <Enum Def> '}'  ';'
 
<Enum Def>     ::= <Enum Val> ',' <Enum Def>
                 | <Enum Val>

<Enum Val>     ::= Id
                 | Id '=' OctLiteral
                 | Id '=' HexLiteral
                 | Id '=' DecLiteral  


! ===================================================================
! Types
! ===================================================================

<Type>     ::= <Base> <Pointers> 

<Base>     ::= <Scalar>
             | struct Id 
             | enum Id
             | '@'Id  
             | delegate Id
             | union Id
             | typeof '(' <Expression> ')'
             
<Scalar>   ::= byte
             | int
             | uint
             | sbyte
             | bool
             | void           
             | string

<Pointers> ::= '*' <Pointers>
             |

! ===================================================================
! Statements
! ===================================================================

<Statement>        ::= <Var Decl>
               | Id ':'                            !Label
               | if '(' <Expression> ')' <Statement>          
               | if '(' <Expression> ')' <Then Stm> else <Statement>      
               ! Preprocess ifdef-ifndef
               | '#'ifdef MacroLiteral <Statement> 
               | '#'ifndef MacroLiteral <Statement> 
               
               | while '(' <Expression> ')' <Statement> 
               | for '(' <Arg> ';' <Arg> ';' <Arg> ')' <Statement>
               | asm '{' <INSTRUCTIONS>  '}'
               | loop <Statement>
               | <Normal Stm>
              

<Then Stm>   ::= if '(' <Expression> ')' <Then Stm> else <Then Stm> 
               | while '(' <Expression> ')' <Then Stm> 
               | for '(' <Arg> ';' <Arg> ';' <Arg> ')' <Then Stm>
               | <Normal Stm>

<Normal Stm> ::= do <Statement> while '(' <Expression> ')'
               | switch '(' <Expression> ')' '{' <Case Stms> '}'
               | <Block>
               | <Expression> ';'               
               | goto Id ';'
               | break ';'
               | continue ';'
               | goto case DecLiteral ';'
               | goto default ';'
               | next ';'
               | interrupt <CONSTANT> ';'
               | return <Expression> ';'
               | ';'              !Null statement



<Arg>       ::= <Expression> 
              | 

<Case Stms> ::= case <Value> ':' <Stm List> <Case Stms>
              | default ':' <Stm List>                  
              |

<Block>     ::= '{' <Stm List> '}' 

<Stm List>  ::=  <Statement> <Stm List> 
              | 


! ===================================================================
! Here begins the C's 15 levels of operator precedence.
! ===================================================================

<Expression>       ::= <Op Assign>

<Op Assign>  ::= <Op If> '='   <Op Assign>
               | <Op If> '<>'   <Op Assign>
               | <Op If> '+='  <Op Assign>
               | <Op If> '-='  <Op Assign>
               | <Op If> '*='  <Op Assign>
               | <Op If> '/='  <Op Assign>
               | <Op If> '^='  <Op Assign>
               | <Op If> '&='  <Op Assign>
               | <Op If> '|='  <Op Assign>
               | <Op If> '>>=' <Op Assign>
               | <Op If> '<<=' <Op Assign>
               | <Op If>

<Op If>      ::= <Op Or> '?' <Op If> ':' <Op If>
               | <Op Or>

<Op Or>      ::= <Op Or> '||' <Op And>
               | <Op And>

<Op And>     ::= <Op And> '&&' <Op BinOR>
               | <Op BinOR>

<Op BinOR>   ::= <Op BinOr> '|' <Op BinXOR>
               | <Op BinXOR>

<Op BinXOR>  ::= <Op BinXOR> '^' <Op BinAND>
               | <Op BinAND>

<Op BinAND>  ::= <Op BinAND> '&' <Op Equate>
               | <Op Equate>

<Op Equate>  ::= <Op Equate> '==' <Op NEqual>
               | <Op NEqual>

<Op NEqual>  ::= <Op NEqual> '!=' <Op Compare>
               | <Op Compare>

<Op Compare> ::= <Op Compare> '<'  <Op Shift>
               | <Op Compare> '>'  <Op Shift>
               | <Op Compare> '<=' <Op Shift>
               | <Op Compare> '>=' <Op Shift>
               | <Op Shift>

<Op Shift>   ::= <Op Shift> '<<' <Op Add>
               | <Op Shift> '>>' <Op Add>
               
               | <Op Shift> '~>' <Op Add>
               | <Op Shift> '<~' <Op Add>
               | <Op Add>

<Op Add>     ::= <Op Add> '+' <Op Mult>
               | <Op Add> '-' <Op Mult>
               | <Op Mult>

<Op Mult>    ::= <Op Mult> '*' <Op Unary>
               | <Op Mult> '/' <Op Unary>
               | <Op Mult> '%' <Op Unary>
               | <Op Unary>

<Op Unary>   ::= '!'    <Op Unary>
               | '~'    <Op Unary>   
               | '-'    <Op Unary>
               | '*'    <Op Unary>
               | '&'    <Op Unary>               
               | '++'   <Op Unary>
               | '--'   <Op Unary>
               | '??'   <Op Unary>
               | '¤'   <Op Unary>
               | <Op Pointer> '++'
               | <Op Pointer> '--'
               | '(' <Type> ')' <Op Unary>   !CAST
               | sizeof '(' <Type> ')'
               | sizeof '(' ID <Pointers> ')'
               | nameof  '(' ID ')'
    
               | <Op Pointer>

<Op Pointer> ::= <Op Pointer> '.' <Value>
               | <Op Pointer> '->' <Value>
               | Id '::' <Value>
               | <Type> '::' <Value>
               | <Value> '.' <Value>
               | <Op Pointer> '[' <Expression> ']'
               | <Value>

<Value>      ::= <CONSTANT>
               | Id '(' <PARAM EXPR> ')'
               | Id '(' ')'          
               | Id
               | '(' <Expression> ')'
               | <REGISTER>

<CONSTANT>    ::= OctLiteral
               | HexLiteral
               | MacroLiteral
               | DecLiteral  
               | StringLiteral
               | CharLiteral
               | BinaryLiteral
               | BooleanLiteral
               | NullLiteral    
                        
<PARAM EXPR>  ::= <Expression> ',' <PARAM EXPR>
               |  <Expression>
               |        
<REGISTER>   ::= AX
              | BX
              | CX
              | DX
              | SI
              | DI
              | SP
              | BP
              | CS
              | DS
              | ES
              | FS
              | GS
              | SS
             
              | AH
              | AL
              | BH
              | BL
              | CH
              | CL
              | DH
              | DL  
                        
<INSTRUCTION> ::= StringLiteral ';'
           
<INSTRUCTIONS> ::= <INSTRUCTION> <INSTRUCTIONS> 
                |             
          
